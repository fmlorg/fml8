dnl
dnl $FML: configure.in,v 1.17 2002/04/20 11:18:21 fukachan Exp $
dnl
AC_REVISION($Revision$)

dnl Process this file with autoconf to produce a configure script.
AC_INIT(fml/libexec/loader.in)

# compile with optimisation and without debugging by default
CFLAGS=${CFLAGS-"-O"}
LDFLAGS=${LDFLAGS-""}

dnl path_check
dnl Checks for programs.

dnl perl is mandatory
AC_CHECK_PROGS(have_perl, perl)
if test ! -n "$have_perl" ; then
   AC_MSG_ERROR("Perl needs to be installed to use fml")
fi
AC_PATH_PROG(PERL,   perl)

AC_MSG_RESULT("checking whether optional useful commands are available or not")

AC_PATH_PROG(CKSUM,    cksum)
AC_PATH_PROG(MD5,      md5)

AC_PATH_PROG(LS,       ls)
AC_PATH_PROG(TAR,      tar)
AC_PATH_PROG(GZIP,     gzip)
AC_PATH_PROG(ZCAT,     zcat)
AC_PATH_PROG(GUNZIP,   gunzip)
AC_PATH_PROG(LHA,      lha)
AC_PATH_PROG(ISH,      ish)
AC_PATH_PROG(BZIP2,    bzip2)
AC_PATH_PROG(BUNZIP2,  bunzip2)
AC_PATH_PROG(UUENCODE, uuencode)
AC_PATH_PROG(COMPRESS, compress)

AC_PATH_PROG(SENDMAIL, sendmail)

AC_PATH_PROG(NKF,      nkf)
AC_PATH_PROG(KAKASI,   kakasi)
AC_PATH_PROG(NAMAZU,   namazu)

AC_PATH_PROG(PGP,      pgp)
AC_PATH_PROG(PGP5,     pgp5)
AC_PATH_PROG(PGPe,     pgpe)
AC_PATH_PROG(PGPK,     pgpk)
AC_PATH_PROG(PGPS,     pgps)
AC_PATH_PROG(PGPV,     pgpv)
AC_PATH_PROG(GPG,      gpg)
dnl path_check_end

use_warning=yes
AC_ARG_WITH(warning, 
[  --without-warning       run perl as perl without -w],
    use_warning=$withval)


fmlconfdir=${sysconfdir}/fml
AC_ARG_WITH(fmlconfdir, 
[  --with-fmlconfdir=DIR   use DIR instead of SYSCONFDIR/fml], 
    fmlconfdir=$withval)
AC_SUBST(fmlconfdir)


mlspooldir=/var/spool/ml
AC_ARG_WITH(mlspooldir, 
[  --with-mlspooldir=DIR   use DIR instead of /var/spool/ml], 
    mlspooldir=$withval)
AC_SUBST(mlspooldir)


fml_owner=fml
AC_ARG_WITH(fml_owner, 
[  --with-fml-owner=USER   use USER instead of fml], 
    fml_owner=$withval)
AC_SUBST(fml_owner)


fml_group=fml
AC_ARG_WITH(fml_group, 
[  --with-fml-group=GROUP  use GROUP instead of fml], 
    fml_group=$withval)
AC_SUBST(fml_group)


_hostname=`hostname`
default_domain=` expr $_hostname : '[[^\.]]*\.\(.*\)' `
AC_ARG_WITH(default_domain, 
[  --with-default-domain=DOMAIN  use DOMAIN as the ML domain], 
    default_domain=$withval)
AC_SUBST(default_domain)


AC_SUBST(SHELL)

AC_OUTPUT(\
	config.sh \
	fml/etc/main.cf \
	fml/etc/default_config.cf.ja \
	fml/etc/site_default_config.cf \
	fml/libexec/loader \
	fml/bin/fmlconf \
	fml/bin/fmldoc \
	fml/bin/fmlhtmlify \
	fml/bin/fmlsch \
	fml/bin/fmlthread \
	fml/bin/fmlspool \
	fml/bin/makefml \
	)

if test "X$use_warning" = "Xyes" ; then
	echo "enabled perl -w"
else
	echo "disabled warning (perl -w)"
	sed '1s/-w//' fml/libexec/loader > fml/libexec/loader.new.$$
	mv fml/libexec/loader.new.$$ fml/libexec/loader
fi

if test -f fml/libexec/loader ; then
   chmod 755 fml/libexec/loader
fi


echo ""
echo "fml has been configure with the following options:"
echo ""
echo "                 prefix: $prefix"
echo "            exec_prefix: $exec_prefix"
echo "                 bindir: $bindir"
echo "                sbindir: $sbindir"
echo "                lib_dir: $libdir"
echo "            libexec_dir: $libexecdir"
echo "                 mandir: $mandir"
echo "                datadir: $datadir"
echo ""
echo "      fml process owner: $fml_owner"
echo "      fml process group: $fml_group"
echo "             fmlconfdir: $fmlconfdir"
echo "             mlspooldir: $mlspooldir"
echo "         default domain: $default_domain"
echo ""
echo "Now you must run \"make install\"."
echo ""
